// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */
CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Not(in=load, out=notLoad);
    And(a=inc, b=notLoad, out=andIncNotLoad);
    Or(a=andIncNotLoad, b=reset, out=sel0);

    Or(a=load, b=reset, out=sel1);

    Inc16(in=regOut, out=incOut);
    Mux4Way16(a=regOut, b=incOut, c=in, d=false, sel[0]=sel0, sel[1]=sel1, out=muxOut);
    Register(in=muxOut, load=true, out=regOut, out=out);
}
